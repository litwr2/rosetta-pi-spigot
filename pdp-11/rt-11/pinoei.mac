;for macro-11 assembler
;it calculates pi-number using the next C-algorithm
;https://crypto.stanford.edu/pbc/notes/pi/code.html

;#include <stdio.h>
;#define N 2800
;main() {
;   long r[N + 1], i, k, b, c;
;   c = 0;
;   for (i = 1; i <= N; i++)   ;it is the fixed line!, the original was (i = 0; i < N; ...
;      r[i] = 2000;
;   for (k = N; k > 0; k -= 14) {
;      d = 0;
;      i = k;
;      for(;;) {
;         d += r[i]*10000;
;         b = i*2 - 1;
;         r[i] = d%b;
;         d /= b;
;         i--;
;         if (i == 0) break;
;         d *= i;
;      }
;      printf("%.4d", (int)(c + d/10000));
;      c = d%10000;
;   }
;}

;the time of the calculation is quadratic, so if T is time to calculate N digits
;then 4*T is required to calculate 2*N digits
;main loop count is 7*(4+D)*D/16, D - number of digits

;litwr has written this for RT-11
;bqt helped much with optimization
;Manwe helped with development
;Thorham and meynaf helped too

      .radix 10

DEBUG = 0
HMUL = 0   ;hardware multiplication, 0 - no
IO = 1
MINUS = 1  ;0 - if dividers are positive, this is ok up to 4680 digits

      .MCall .exit, .print, .gtim, .ttyin, .ttyout, .ttinr, .settop, .gval
      $JSW =: ^O44
      TTSPC$ =: ^O10000
      CONFIG = ^O300

kv = kvs + 2

.macro div0s l0
     rol r3
     rol r2
     add r0,r2
     bcc l0
     .endm

.macro div0a l0
     rol r3
     rol r2
     add r1,r2
     bcs l0
     .endm

.macro div0z
     div0s 101$
     div0s 102$
202$:  div0s 103$
203$:  div0s 104$
204$:  div0s 105$
205$:  div0s 106$
206$:  div0s 107$
207$:  div0s 108$
208$:  div0s 109$
209$:  div0s 110$
210$:  div0s 111$
211$:  div0s 112$
212$:  div0s 113$
213$:  div0s 114$
214$:  div0s 115$
215$:  div0s 100$
200$:  rol r3
     br 80$

101$:  div0a 202$
102$:  div0a 203$
103$:  div0a 204$
104$:  div0a 205$
105$:  div0a 206$
106$:  div0a 207$
107$:  div0a 208$
108$:  div0a 209$
109$:  div0a 210$
110$:  div0a 211$
111$:  div0a 212$
112$:  div0a 213$
113$:  div0a 214$
114$:  div0a 215$
115$:  div0a 200$
100$:  rol r3
     add r1,r2
80$:
     .endm

.macro div32x16     ;R4:R3 = R2:R3/R1, R2 = R2:R3%R1, used: R0, R2, R3, R4
                    ;may work wrong if R1>$7fff
     cmp r2,r1
     bcs .+6
     jmp @#divlong

t1:  div0z
     clr r4
     .endm

.macro divl32x16
     mov r3,-(sp)
;OPT = 5         ;It's a constant for the pi-spigot
     asl r2
     asl r2
     asl r2
     asl r2
     asl r2

     mov r2,r3
     clr r2

    div0s 106$
    div0s 107$
207$: div0s 108$
208$:  div0s 109$
209$:  div0s 110$
210$:  div0s 111$
211$:  div0s 112$
212$:  div0s 113$
213$:  div0s 114$
214$:  div0s 115$
215$:  div0s 100$
200$:  rol r3
     br 70$

106$: div0a 207$
107$: div0a 208$
108$:  div0a 209$
109$:  div0a 210$
110$:  div0a 211$
111$:  div0a 212$
112$:  div0a 213$
113$:  div0a 214$
114$:  div0a 215$
115$:  div0a 200$
100$:  rol r3
     add r1,r2
70$:

;t3:  div0z  ;OPT=0

     mov r3,r4
     mov (sp)+,r3

t2:  div0z
     .endm

.macro div0 ?l0 ?l1
     asl r3
     rol r2
     bcs l1            ;for r1 > $7fff

     cmp r2,r1
     bcs l0

l1:  sub r1,r2
     inc r3
l0:
     .endm

.macro divm32x16 ?div32
           ;R4:R3 = R2:R3/R1, R2 = R2:R3%R1, used: R0, R2, R3, R4
     cmp r2,r1
     bcc div32

     .rept 16
     div0
     .endm
     clr r4
     jmp @#enddm

div32:
     mov r3,-(sp)

;OPT = 5         ;It's a constant for the pi-spigot
     .rept 5
     asl r2
     .endm

     mov r2,r3
     clr r2

     .rept 16-5  ;OPT=5
     div0
     .endm

     mov r3,r4
     mov (sp)+,r3

     .rept 16
     div0
     .endm
     .endm

START:
         bis #TTSPC$,@#$JSW
         .settop #-2
         sub #ra-2,r0
         mov r0,r2
         clr r3
         mov #7,r1
         call @#div32x16s
         bic #3,r2
         mov r2,@#maxnum
         .print #msg1
         ;mov @#maxnum,r2
         call PR0000
         .print #msg2
         call @#getnum
         .print #eol

         mov r2,r4
         add #3,r4
         bic #3,r4
         cmp r2,r4
         beq 7$

         mov r4,r2
         call @#PR0000
         .print #msg3

7$:      mov r4,r0
         asr r4
         add r0,r4
         asl r0
         add r0,r4   ;r4 <- r4/2*7
         mov r4,@#kv
         tst -(sp)       ;create a location for high(d)

         .gtim #area,#time
         mov r4,r0   ;fill r-array
         mov #2000,r1
         mov #ra+2,r2
1$:      mov r1,(r2)+
         sob r0,1$

mloop:   clr r5       ;d <- 0
         clr @sp
kvs:     mov #0,r1
         asl r1       ;i <- 2k
ivs:
.if eq HMUL
         mov ra(r1),r0     ;r[i]
         clr r4            ;r[i]*10000
         clr r2
         mov r0,r3         ;the result in r2 - high, r3 - low
         asl r3
         rol r2
         asl r3
         rol r2
         asl r3
         rol r2
         sub r3,r0
         sbc r4
         sub r2,r4
         asl r3
         rol r2
         sub r3,r0
         sbc r4
         sub r2,r4
         sub r3,r0
         sbc r4
         sub r2,r4
         swab r0
         swab r4
         clrb r4
         bisb r0,r4
         clrb r0
         sub r0,r3
         sbc r2
         sub r4,r2
.iff
         mov #10000,r2
         mul ra(r1),r2
.if ne MINUS
         bpl .+6
         add #10000,r2
.endc
.endc
         add r3,r5
         mov r5,r3
         adc r2
         add @sp,r2   ;sets CF=0
         mov r2,@sp
         dec r1          ;b <- 2*i-1, CF=0 for EIS!
.if ne MINUS
         bpl .+6
         jmp @#divminus
.endc
         mov r1,r0
         neg r0
         div32x16
enddm:
         mov r2,ra+1(r1)      ;r[i] <- d%b
         dec r1        ;i <- i - 1
         beq 4$

         add r3,r2       ;d <- d/b*i
         adc r4
         mov @sp,r3
         sub r2,r5
         sbc r3
         sub r4,r3
         ror r3
         ror r5
         mov r3,@sp
         jmp @#ivs
4$:
.if ne IO
         mov r3,r2
         mov r4,r3
         mov #10000,r1
         call @#div32x16s
         add @#cv,r2  ;c + d/10000
         mov r3,@#cv     ;c <- d%10000
         call @#PR0000
.endc
         sub #14,@#kv      ;k <- k - 14
         beq .+6
         jmp @#mloop

         mov #time2,r1
         .gtim #area,r1
         mov #32,r0
         .ttyout
         sub @#time+2,@#time2+2
         sbc @#time2
         sub @#time,@#time2
         mov @#time2,r3
         mov @#time2+2,r2
         asl r2
         rol r3
         .gval #area,#CONFIG
         bit #32,r0    ;50 or 60 Hz?
         bne 206$

         asl r2     ;*5
         rol r3
         add @#time2+2,r2
         adc r3
         add @#time2,r3
         mov #3,r1
         call @#div32x16s
         asr r3
         add r3,r2
         mov r1,r3
         adc r3
206$:    mov r3,r0
         mov r2,r1
         call @#printsec   ;prints r0:r1
         tst (sp)+       ;clear the location for high(d)
         .ttinr

         bic #TTSPC$,@#$JSW
         .exit

.if ne MINUS
divminus: divm32x16
          jmp @#enddm
.endc
divlong:  divl32x16
          jmp @#enddm

div32x16s: ;R1:R2 = R3:R2/R1, R3 = R3:R2%R1, used: R0,R4
           ;compact form - 64 bytes
     cmp r3,r1
     bcc 32$

     call @#3$
     clr r1
     return

32$: mov r2,r0
     mov r3,r2
     clr r3
     call @#3$
     mov r2,r4
     mov r0,r2
     call @#3$
     mov r4,r1
     return

3$:  call @#.+4
     call @#.+4
     call @#.+4
     call @#.+4
     asl r2
     rol r3
     bcs 22$    ;for R1>$7fff

     cmp r3,r1
     bcs 20$

22$: sub r1,r3
     inc r2
20$:  return

.if ne DEBUG
PRX:mov r0,-(sp)   ;prints pdata, doesn't save flags
    mov r2,-(sp)
    mov r3,-(sp)
    mov @#pdata,r2
   call PRALL
   mov (sp)+,r3
   mov (sp)+,r2
   mov (sp)+,r0
      return

PRC:  mov r0,-(sp)
   mov @#pdata,r0
   .ttyout
   mov (sp)+,r0
      return

pdata: .word 0

PRALL:    ;prints r2, used: r0,r2,r3
        mov #10000,r3
	CALL @#PRZ
.endc

PR0000:
        mov #1000,r3
	CALL @#PRZ
        mov #100,r3
	CALL @#PRZ
        mov #10,r3
	CALL @#PRZ
	mov r2,r0
PR:	add #48,r0
   	.ttyout
        return

PRZ:	mov #65535,r0
4$:	inc r0
	cmp r2,r3
	bcs PR

	sub r3,r2
	br 4$

printsec:  ;prints R0:R1/100
        mov r1,r2
        mov r0,r1
        clr r4
        mov #1,r5 
        mov #34464,r3  ;100000-65536
        call @#20$
        clr r5 
        mov #10000,r3
        call @#20$
        mov #1000,r3
        call @#20$
        inc r4
        mov #100,r3
        call @#20$
        movb #'.,r0
        .ttyout
        mov #10,r3
        call @#20$
        mov r2,r0
2$:     add #48,r0
        .ttyout
        inc r4
5$:     return

7$:     tst r4
        bne 2$

        tst r0
        beq 5$

        inc r4
        br 2$

20$:    mov #65535,r0
4$:	inc r0
        cmp r1,r5
        bcs 7$
        bne 8$

	cmp r2,r3
	bcs 7$

8$:     sub r3,r2
        sbc r1
        sub r5,r1
	br 4$

cv: .word 0
time: .word 0,0   ;high, low!
ra:
time2: .word 0,0
maxnum: .word 0
area: .word 0,0

getnum: clr r1    ;length
        clr r2    ;number
20$:     .ttyin
        cmp #13,r0
        beq 5$

        cmp #127,r0   ;backspace
        beq 1$

        cmp #47,r0
        bcc 20$

        cmp #48+9,r0
        bcs 20$

        cmp #4,r1
        beq 20$

        mov r2,-(sp)
        .ttyout
        inc r1
        sub #48,r0
        mov r2,r3
        asl r3
        asl r3
        add r3,r2
        asl r2
        add r0,r2
        br 20$

1$:     tst r1
        beq 20$

        dec r1
        .print #delstr
        mov (sp)+,r2
        br 20$

5$:     tst r1
        beq 20$

        cmp @#maxnum,r2
        bcs 20$

        tst r2
        beq 20$

8$:     mov (sp)+,r0
        sob r1,8$
        return

msg1: .ascii "number pi"
      ;.byte 160 ;Greek pi
      .ascii " calculator v11 (no EIS"
.if ne HMUL
      .ascii ", HMUL"
.endc
      .ascii ")" <13> <10>
      .ascii "number of digits (up to " <128>
msg2: .ascii ")? " <128>
msg3: .ascii " digits will be printed"
eol: .byte 0
delstr: .byte 8,32,8,128
;    .even

.End	START

