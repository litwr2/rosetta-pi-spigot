;for macro-11 assembler
;it calculates pi-number using the next C-algorithm
;https://crypto.stanford.edu/pbc/notes/pi/code.html

;#include <stdio.h>
;#define N 2800
;main() {
;   long r[N + 1], i, k, b, c;
;   c = 0;
;   for (i = 1; i <= N; i++)  ;it is the fixed line!, it was (i = 0; i < N; ...
;      r[i] = 2000;
;   for (k = N; k > 0; k -= 14) {
;      d = 0;
;      i = k;
;      for(;;) {
;         d += r[i]*10000;
;         b = i*2 - 1;
;         r[i] = d%b;
;         d /= b;
;         i--;
;         if (i == 0) break;
;         d *= i;
;      }
;      printf("%.4d", (int)(c + d/10000));
;      c = d%10000;
;   }
;}

;the time of the calculation is quadratic, so if T is time to calculate N digits
;then 4*T is required to calculate 2*N digits
;main loop count is 7*(4+D)*D/16, D - number of digits

;litwr has made this code
;bqt has made the first RSX-11 version and helped much with optimization
;the idea of fast division was discovered by blackmirror
;ivagor gave some useful information
;Thorham and meynaf helped to make fast multiplication by 10000

	.TITLE	PI - Spigot PI calculator not using EIS

	.MCALL	QIOW$S, EXIT$S, GTIM$, DIR$
	.MCALL	QIOW$, WTSE$, QIO$

      .radix 10

MEMSZ = 31584 ;this is a constant calculated from data in a MAP-file produced
        ;by a linker, 10500 is enough for 3000 digits
        ;31584 for the 11/93 under RSX-11M+, 31400 for the Pro under P/OS
IO = 1
MINUS = 1  ;0 - if dividers are positive, this is ok up to 4680 digits

kv = kvs + 2

.macro div0s l0
     rol r3
     rol r2
     add r0,r2
     bcc l0
     .endm

.macro div0a l0
     rol r3
     rol r2
     add r1,r2
     bcs l0
     .endm

.macro div0z
     div0s 101$
     div0s 102$
202$:  div0s 103$
203$:  div0s 104$
204$:  div0s 105$
205$:  div0s 106$
206$:  div0s 107$
207$:  div0s 108$
208$:  div0s 109$
209$:  div0s 110$
210$:  div0s 111$
211$:  div0s 112$
212$:  div0s 113$
213$:  div0s 114$
214$:  div0s 115$
215$:  div0s 100$
200$:  rol r3
     br 80$

101$:  div0a 202$
102$:  div0a 203$
103$:  div0a 204$
104$:  div0a 205$
105$:  div0a 206$
106$:  div0a 207$
107$:  div0a 208$
108$:  div0a 209$
109$:  div0a 210$
110$:  div0a 211$
111$:  div0a 212$
112$:  div0a 213$
113$:  div0a 214$
114$:  div0a 215$
115$:  div0a 200$
100$:  rol r3
     add r1,r2
80$:
     .endm

.macro div32x16     ;R4:R3 = R2:R3/R1, R2 = R2:R3%R1, used: R0, R2, R3, R4
                    ;may work wrong if R1>$7fff
     cmp r2,r1
     bcs .+6
     jmp @#divlong

z1:  div0z
     clr r4
     .endm

.macro divl32x16
     mov r3,-(sp)
;OPT = 5         ;It's a constant for the pi-spigot
     asl r2
     asl r2
     asl r2
     asl r2
     asl r2

     mov r2,r3
     clr r2

    div0s 106$
    div0s 107$
207$: div0s 108$
208$:  div0s 109$
209$:  div0s 110$
210$:  div0s 111$
211$:  div0s 112$
212$:  div0s 113$
213$:  div0s 114$
214$:  div0s 115$
215$:  div0s 100$
200$:  rol r3
     br 70$

106$: div0a 207$
107$: div0a 208$
108$:  div0a 209$
109$:  div0a 210$
110$:  div0a 211$
111$:  div0a 212$
112$:  div0a 213$
113$:  div0a 214$
114$:  div0a 215$
115$:  div0a 200$
100$:  rol r3
     add r1,r2
70$:

;z3:  div0z  ;OPT=0

     mov r3,r4
     mov (sp)+,r3

z2:  div0z
     .endm

.macro div0 ?l0 ?l1
     asl r3
     rol r2
     bcs l1            ;for r1 > $7fff

     cmp r2,r1
     bcs l0

l1:  sub r1,r2
     inc r3
l0:
     .endm

.macro divm32x16 ?div32
           ;R4:R3 = R2:R3/R1, R2 = R2:R3%R1, used: R0, R2, R3, R4
     cmp r2,r1
     bcc div32

     .rept 16
     div0
     .endm
     clr r4
     jmp @#enddm

div32:
     mov r3,-(sp)

;OPT = 5         ;It's a constant for the pi-spigot
     .rept 5
     asl r2
     .endm

     mov r2,r3
     clr r2

     .rept 16-5  ;OPT=5
     div0
     .endm

     mov r3,r4
     mov (sp)+,r3

     .rept 16
     div0
     .endm
     .endm

	.PSECT	CODE,I,RW
START:
    mov #msg1m,r5
    mov #MAXD,r2
    call PR0000
    QIOW$S	#IO.WLB, #5, #5,,,,<#msg1, #msg1m-msg1, #'$>
    MOV	#msg1m,R0
	MOV	#7,R1
	QIOW$S	#IO.RPR,#5,#5,,#IOSB,,<#OBUF,#8,,R0,R1>
	TSTB	IOSB
	BPL	10$

    QIOW$S	#IO.WLB, #5, #5,,,,<#msg2, #msg3-msg2, #40>
	EXIT$S

10$:	MOV	IOSB+2,R0		; Terminate input with NUL.
	CLRB	OBUF(R0)

	MOV	#OBUF,R0			; Try to parse input as number.
	CALL	$CDTB
	TST	R2			; Did we stop at NUL?
	BEQ	20$			; Yes.

	QIOW$S	#IO.WLB, #5, #5,,,,<#msg3, #msg4-msg3, #40>
	EXIT$S

20$:	CMP	R1,#MAXD		; Check value against max.
	BLOS	30$

	QIOW$S	#IO.WLB, #5, #5,,,,<#msg4, #msg5-msg4, #40>
	EXIT$S

30$:
	MOV	R1,R0			; Round value to even four.
	ADD	#3,R0
	BIC	#3,R0
	CMP	R0,R1			; Did that cause a change?
	BEQ	31$

    mov r0,r2
    mov r0,-(sp)
    mov #msg5,r5
    call PR0000
    QIOW$S	#IO.WLB, #5, #5,,,,<#msg5, #msg6-msg5, #40>
    mov (sp)+,r0

31$:
    QIOW$S  #IO.WLB, #5, #5,,,,<#msg5, #1, #40>
SPIGOT:	 mov r0,r4
         asr r4
         add r0,r4
         asl r0
         add r0,r4    ;r4 <- r0/2*7
         mov r4,@#kv
         tst -(sp)

	     DIR$ #TSTART		;Start measure time
         mov r4,r0     ;fill r-array
         mov #2000,r1
         mov #ra+2,r2
1$:      mov r1,(r2)+
         sob r0,1$

mloop:   clr r5       ;d <- 0
         clr @sp
kvs:     mov #0,r1
         asl r1       ;i <- 2k
ivs:     mov ra(r1),r0     ;r[i]
         clr r4            ;r[i]*10000
         clr r2
         mov r0,r3         ;the result in r2 - high, r3 - low
         asl r3
         rol r2
         asl r3
         rol r2
         asl r3
         rol r2
         sub r3,r0
         sbc r4
         sub r2,r4
         asl r3
         rol r2
         sub r3,r0
         sbc r4
         sub r2,r4
         sub r3,r0
         sbc r4
         sub r2,r4
         swab r0
         swab r4
         clrb r4
         bisb r0,r4
         clrb r0
         sub r0,r3
         sbc r2
         sub r4,r2
         add r3,r5
         mov r5,r3
         adc r2
         add @sp,r2   ;sets CF=0
         mov r2,@sp
         dec r1          ;b <- 2*i-1, CF=0 for EIS!
.if ne MINUS
         bpl .+6
         jmp @#divminus
.endc

         mov r1,r0
         neg r0
         div32x16
enddm:
         mov r2,ra+1(r1)      ;r[i] <- d%b
         dec r1        ;i <- i - 1
         beq 34$

         add r3,r2       ;d <- d/b*i
         adc r4
         mov @sp,r3
         sub r2,r5
         sbc r3
         sub r4,r3
         ror r3
         ror r5
         mov r3,@sp
         jmp @#ivs

34$:
.if ne IO
         mov r3,r2
         mov r4,r1
         mov #10000,r0
         call $DDIV
         add @#cv,r2  ;c + d/10000
         mov r0,@#cv     ;c <- d%10000
         MOV #OBUF,R5
         DIR$ #WAIT
	     call PR0000
	     DIR$ #WRIT
.endc
         sub #14,@#kv      ;k <- k - 14
         beq 5$
         jmp @#mloop

5$:
         DIR$ #WAIT
	     DIR$ #TSTOP
         tst (sp)+
;
; Figure out how many ticks passed, and make this
; into 1/100s.
;

;
; First part, find out how many ticks...
;
	MOV	T2+12,R4	; Get ticks.
	SUB	T1+12,R4	; Calculate delta.
	BCC	2$

	ADD	T2+14,R4	; If that was negative, we had wrap. Adjust.
	DEC	T2+10		; And then we have one less second.
2$:
;
; Second part. Convert to 1/100s.
;
    MOV R4,R0
    MOV #100,R1
    call $MUL    ;R0/R1 = R0:R1
    mov r1,r2
    mov r0,r1
    mov T2+14,r0
    call $DDIV   ;R1:R2/R0 = R1:R2 mod R0
    mov r2,r4
;
; Now get minutes passed.
;
	MOV	T2+8,R1	; Get end minute.
	SUB	T1+8,R1	; Substract start minute.
	BGE	3$		; If result is >= 0 we're good.

	ADD	#60,R1		; It was less. Adjust minutes.
3$:
;
; Get seconds passed.
;
	MOV	T2+10,R0	; Get end second.
	SUB	T1+10,R0	; Substract start second.
	BGE	4$

	ADD	#60,R0		; If negative, adjust up.
	DEC	R1		; And one minute less.
;
; Now make seconds...
;
4$:	mov r0,-(sp)
    mov #60,r0
    call $mul           ;Minutes to seconds
	ADD	(sp)+,R1		;Add in seconds
    mov #ra,r0
    MOVB #32,(r0)+      ;a white space
    MOV #<5*2048>+10,R2	;5 places, base 10
    CALL	$CBTA
    movb #'.,(r0)+
    MOV	R4,R1
	MOV #<2*2048>+512+10,R2	;2 digits, base 10, with leading 0
	CALL	$CBTA
    sub #ra,r0
    QIOW$S	#IO.WLB,#5, #5,,,,<#ra, r0>
	EXIT$S

.if ne MINUS
divminus: divm32x16
          jmp @#enddm
.endc
divlong:  divl32x16
          jmp @#enddm

; Output 4 digits
;
; In: R5 - Buffer pointer
;     R2 - Value
PR0000:
    mov #1000,r3
	CALL @#PRZ
    mov #100,r3
	CALL @#PRZ
    mov #10,r3
	CALL @#PRZ
	mov r2,r0
PR:	add #48,r0
   	movb r0,(r5)+
    return

PRZ:	mov #65535,r0
4$:	inc r0
	cmp r2,r3
	bcs PR

	sub r3,r2
	br 4$

T1:	.BLKW	8		;Buffers for time stamps
T2:	.BLKW	8

MAXD=<<MEMSZ/7>/4>*8			;Max # of digits we allow

cv:	.WORD	0
kv = kvs + 2

TSTART:	GTIM$	T1		;Timer start.
TSTOP:	GTIM$	T2		;Timer stop.
EVNTFLG = 5
WRIT:	QIO$	IO.WAL,5,EVNTFLG,,,,<OBUF,4>
WAIT:	WTSE$	EVNTFLG

IOSB:	.BLKW	2
OBUF:	.BLKB	8

ra:
msg1:  .ascii "number pi calculator v2 (no EIS)"<10><13>
       .ascii "number of digits (up to "
msg1m: .ascii "0000)? "
msg2:  .ascii "Error in read"
msg3:  .ascii "Non-digit character in input"
msg4:  .ascii "Number too large"
msg5:  .ascii "0000 digits will be printed"
msg6:
memfill: .blkb MEMSZ
       .blkb MEMSZ
eom:
	.END	START
